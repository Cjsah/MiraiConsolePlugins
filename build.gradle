plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.5.10'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = project.plugin_version
group = project.mvn_group
if (project.build_number != "undefined") {
    version += "+build.${project.build_number}"
}

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.cjsah.net/' }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib"
    compile "net.cjsah.console:MiraiBotConsole:${project.console_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("plugin.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
//			  groupId project.group
//            version project.version
            artifactId project.archivesBaseName
            from components.java
            artifact sourcesJar
        }
    }

    def ENV = System.getenv()
    if (ENV.MAVEN_URL) {
        repositories.maven {
            url ENV.MAVEN_URL
            credentials {
                username ENV.MAVEN_USERNAME
                password ENV.MAVEN_PASSWORD
            }
        }
    }
}
